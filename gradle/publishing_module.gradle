/*
 * Copyright 2017-2020 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license.
 */

// Configures publishing of Maven artifacts to Bintray

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

apply from: project.rootProject.file('gradle/pom.gradle')

def signingKey = System.getenv("signingKey") ?: ""
def signingPassword = System.getenv("signingPassword") ?: ""

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    archiveClassifier = 'javadoc'
}

afterEvaluate { Project project ->
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom.withXml(configureMavenCentralMetadata)
            }
            pluginMaven(MavenPublication) {
                pom.withXml(configureMavenCentralMetadata)
            }
        }
        repositories {
            maven {
                url = "https://maven.pkg.github.com/komune-io/${project.rootProject.name}"
                credentials {
                    username System.getenv("GITHUB_PKG_MAVEN_USERNAME")
                    password System.getenv("GITHUB_PKG_MAVEN_TOKEN")
                }
            }
        }
    }
    signing {
        def key = signingKey
        def password = signingPassword
        useInMemoryPgpKeys(key, password)
        sign publishing.publications.mavenJava
    }

}