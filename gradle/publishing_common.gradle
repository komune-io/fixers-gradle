ext {
    signingKey = System.getenv("GPG_SIGNING_KEY") ?: ""
    signingPassword = System.getenv("GPG_SIGNING_PASSWORD") ?: ""

    repo = System.getenv("PKG_MAVEN_REPO") // github || sonatype_oss

    repoUsername = repo == "github" ? System.getenv("PKG_GITHUB_USERNAME") : System.getenv("PKG_SONATYPE_OSS_USERNAME")
    repoPassword = repo == "github" ? System.getenv("PKG_GITHUB_TOKEN") : System.getenv("PKG_SONATYPE_OSS_TOKEN")

    githubRepoUrl = "https://maven.pkg.github.com/komune-io/${project.rootProject.name}"
    releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
    snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
    repoUrl = repo == "github" ? githubRepoUrl : (version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl)
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    archiveClassifier = 'javadoc'
}
